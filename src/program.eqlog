Pred ConflictingTypes: ();
Pred ConflictingVariables: ();

// # The AST, encoded algebraically/relationally.
Sort ModuleNode;

Sort TypeNode;
Sort TypeNodeOpt;

Sort ExprNode;
Sort ExprNodeList;

Sort StmtNode;
Sort StmtNodeList;

Sort FunctionNode;

Sort Var;
Sort StringLiteral;
Sort NumberLiteral;

Sort ArgList;

// Module(node, top_lvl_stmts)
Pred Module : ModuleNode * StmtNodeList;

Pred VoidTypeNode : TypeNode;
Pred BooleanTypeNode : TypeNode;
Pred NumberTypeNode : TypeNode;
Pred StringTypeNode : TypeNode;
// FunctionTypeNode(node, domain, codomain)
Pred FunctionTypeNode : TypeNode * ArgList * TypeNode;

Pred VariableExprNode : ExprNode * Var;
Pred VoidExprNode : ExprNode;
Pred FalseExprNode : ExprNode;
Pred TrueExprNode : ExprNode;
Pred StringLiteralExprNode : ExprNode * StringLiteral;
Pred NumberLiteralExprNode : ExprNode * NumberLiteral;
// AppExprNode(node, func, args)
Pred AppExprNode : ExprNode * ExprNode * ExprNodeList;
Pred FunctionExprNode : ExprNode * FunctionNode;

Pred ExprStmtNode : StmtNode * ExprNode;
// LetStmtNode(node, var, initial_value)
Pred LetStmtNode : StmtNode * Var * TypeNodeOpt * ExprNode;
// ReturnStmtNode(node, return_value)
Pred ReturnStmtNode : StmtNode * ExprNode;
Pred ReturnVoidStmtNode : StmtNode;
Pred FunctionStmtNode : StmtNode * FunctionNode;
// IfStmtNode(node, condition, true_branch, false_branch)
Pred IfStmtNode : StmtNode * ExprNode * StmtNodeList * StmtNodeList;
// WhileStmtNode(node, condition, body)
Pred WhileStmtNode : StmtNode * ExprNode * StmtNodeList;

// Function(node, function_name, domain, codomain, body)
Pred Function : FunctionNode * Var * ArgList * TypeNodeOpt * StmtNodeList;

// ## <Sort>NodeList operators.
Pred NilExprNodeList : ExprNodeList;
Pred ConsExprNodeList : ExprNodeList * ExprNode * ExprNodeList;

Pred NilStmtNodeList : StmtNodeList;
Pred ConsStmtNodeList : StmtNodeList * StmtNode * StmtNodeList;

// ## ArgList operators.
Pred NilArgList : ArgList;
Pred ConsArgList : ArgList * Var * TypeNodeOpt * ArgList;

// ## TypeNodeOpt operators.
Pred NoneTypeNodeOpt : TypeNodeOpt;
Pred SomeTypeNodeOpt : TypeNodeOpt * TypeNode;


// # Semantic types.

// ## Simple (i.e. mono) types.
Sort Type;
Sort TypeList;

// ## Semantic type operators.
Func VoidType : Type;
Func BooleanType : Type;
Func NumberType : Type;
Func StringType : Type;
// FunctionType(node, domain, codomain)
Func FunctionType : TypeList * Type -> Type;
// Inverses to FunctionType:
// - DomainTypes(FunctionType(domain, _)) = domain
// - CodomainType(FunctionType(_, codomain)) = codomain
Func DomainTypes : Type -> TypeList;
Func CodomainType : Type -> Type;

Axiom DomainTypes(tau)! => CodomainType(tau)!;
Axiom CodomainType(tau)! => DomainTypes(tau)!;
Axiom DomainTypes(FunctionType(domain, _)) ~> domain;
Axiom CodomainType(FunctionType(_, codomain)) ~> codomain;
Axiom FunctionType(DomainTypes(tau), CodomainType(tau)) ~> tau;

// ## TypeList operators.
// We're equating TypeList elements sometimes. In well-typed programs, this is only possible if the
// type lists have the same length and are made up of the same Types.
Func NilTypeList : TypeList;
Func ConsTypeList : Type * TypeList -> TypeList;
Axiom NilTypeList() = ConsTypeList(_, _) => ConflictingTypes();
Axiom ConsTypeList(head_0, tail_0) = ConsTypeList(head_1, tail_1) => head_0 = head_1 & tail_0 = tail_1;

// ## Joint injectivity of type operators.
Axiom VoidType() = BooleanType() => ConflictingTypes();
Axiom VoidType() = NumberType() => ConflictingTypes();
Axiom VoidType() = StringType() => ConflictingTypes();
Axiom VoidType() = FunctionType(_, _) => ConflictingTypes();

Axiom BooleanType() = NumberType() => ConflictingTypes();
Axiom BooleanType() = StringType() => ConflictingTypes();
Axiom BooleanType() = FunctionType(_, _) => ConflictingTypes();

Axiom NumberType() = StringType() => ConflictingTypes();
Axiom NumberType() = FunctionType(_, _) => ConflictingTypes();

Axiom StringType() = FunctionType(_, _) => ConflictingTypes();

Axiom
    FunctionType(domain_0, codomain_0) = FunctionType(domain_1, codomain_1)
    =>
    domain_0 = domain_1
    & codomain_0 = codomain_1
    ;

// ## Functions assigning to node elements their corresponding semantic Type elements.
Func SemanticType : TypeNode -> Type;
Func SemanticTypeOpt : TypeNodeOpt -> Type;
Func SemanticArgTypes : ArgList -> TypeList;

Axiom tn: TypeNode => SemanticType(tn)!;
Axiom tno: TypeNodeOpt => SemanticTypeOpt(tno)!;
Axiom ag: ArgList => SemanticArgTypes(ag)!;

// Since another axiom enforces that SemanticType is total, these rules will fire for all
// TypeNodes.
Axiom VoidTypeNode(tn) => VoidType() ~> SemanticType(tn);
Axiom BooleanTypeNode(tn) => BooleanType() ~> SemanticType(tn);
Axiom NumberTypeNode(tn) => NumberType() ~> SemanticType(tn);
Axiom StringTypeNode(tn) => StringType() ~> SemanticType(tn);
// Other axioms assert that SemanticArgTypes and SemanticType are total functions, so this rule
// will fire for al FunctionTypeNodes.
Axiom
    FunctionTypeNode(tn, args, codomain)
    =>
    FunctionType(SemanticArgTypes(args), SemanticType(codomain))
    ~>
    SemanticType(tn)
    ;

// SemanticTypeOpt is total, but only for SomeTypeNodeOpt elements does it have a determined type.
// If no other rules apply, it will be an opaque Type element determined only by the TypeNodeOpt
// element.
Axiom
	  SomeTypeNodeOpt(tno, tn)
    =>
    SemanticType(tn)
    ~>
		SemanticTypeOpt(tno)
		;

Axiom NilArgList(al) => NilTypeList() ~> SemanticArgTypes(al);
Axiom
    ConsArgList(al, _, head_type, tail)
    =>
    ConsTypeList(SemanticTypeOpt(head_type), SemanticArgTypes(tail))
    ~>
    SemanticArgTypes(al)
    ;

// ## Determined types, i.e. those that do not contain unsolved type variables.
//
// In well-formed programs, every type should be determined.
//
// Without polymorphism, for example the following program contains an
// undetermined type:
//
//   function foo(x) {}
//
// Here the type of `x` is not determined. Applying the function at least once on an argument with
// determined type will determine the argument type of `foo` though, for example if we add the
// statement `foo(5);`
//
// With polymorphism the above example doesn't work, since `foo` will have type `forall a. a ->
// unit`. However, unsolved type variables remain a problem because we require variables introduced
// using `let` to have a mono/simple type. Consider the following example program:
//
//   function foo() {
//     return foo();
//   }
//
//   let x = foo();
//
// Here `foo`  has type `forall a. () -> a`. Thus, `x` must  be simple type, but it can be any
// simple type: The type of `x` is not determined.

Pred DeterminedType : Type;
Pred DeterminedTypes : TypeList;

// A type list is determined if all of the types it contains are determined.
Axiom sigmas = NilTypeList() => DeterminedTypes(sigmas);
Axiom
    sigmas = ConsTypeList(head, tail)
    & DeterminedType(head)
    & DeterminedTypes(tail)
    =>
    DeterminedTypes(sigmas)
    ;

Axiom sigma = VoidType() => DeterminedType(sigma);
Axiom sigma = BooleanType() => DeterminedType(sigma);
Axiom sigma = NumberType() => DeterminedType(sigma);
Axiom sigma = StringType() => DeterminedType(sigma);
Axiom
    sigma = FunctionType(dom, cod)
    & DeterminedTypes(dom)
    & DeterminedType(cod)
    =>
    DeterminedType(sigma)
    ;
Axiom
    sigmas = DomainTypes(kappa)
    & DeterminedType(kappa)
    =>
    DeterminedTypes(sigmas)
    ;
Axiom
    tau = CodomainType(kappa)
    & DeterminedType(kappa)
    =>
    DeterminedType(tau)
    ;

Axiom
    ConsArgList(_, _, tno, _)
    & t = SemanticTypeOpt(tno)
    =>
    DeterminedType(t)
    ;
Axiom
    Function(_, _, _, codomain_tno, _)
    & t = SemanticTypeOpt(codomain_tno)
    =>
    DeterminedType(t)
    ;

// ## Well-founded types
//
// Consider the following (invalid) function definition:
//
//   function foo (x) {
//       x(x);
//   }
//
// If a denotes the type of x, then a must be equal to the function type a => a, which is
// unsatisfiable. To issue an error in cases such as this one, we introduce a binary predicate
// SmallerType such that SmallerType(sigma, tau) holds if and only if sigma is structurally
// strictly smaller than tau.  If we find that a type must be structurally smaller than itself,
// then we generate an error.

Pred SmallerType : Type * Type;
Axiom SmallerType(sigma, tau) & SmallerType(tau, kappa) => SmallerType(sigma, kappa);

Axiom SmallerType(sigma, sigma) => ConflictingTypes();

// SmallerTypes(sigmas, tau) <=> forall sigma in sigma. SmallerType(sigma, tau)
Pred SmallerTypes : TypeList * Type;
Axiom sigmas = NilTypeList() & tau : Type => SmallerTypes(sigmas, tau);
Axiom
    SmallerTypes(ConsTypeList(head, tail), tau)
    =>
    SmallerType(head, tau)
    & SmallerTypes(tail, tau)
    ;
Axiom
    SmallerType(head, tau)
    & SmallerTypes(tail, tau)
    & sigmas = ConsTypeList(head, tail)
    =>
    SmallerTypes(sigmas, tau)
    ;

// All domain types and the codomain of a function type are smaller than the function type.
Axiom kappa = FunctionType(sigmas, tau) => SmallerTypes(sigmas, kappa) & SmallerType(tau, kappa);

// # Type contexts.
Sort TypeContext;

Pred TypeInContext : Type * TypeContext;
Pred TypesInContext : TypeList * TypeContext;

Axiom gamma : TypeContext & NilTypeList()! => TypesInContext(NilTypeList(), gamma);
Axiom
    sigmas = ConsTypeList(sigmas_head, sigmas_tail)
    & TypeInContext(sigmas_head, gamma)
    & TypesInContext(sigmas_tail, gamma)
    =>
    TypesInContext(sigmas, gamma)
	  ;

Axiom
    & TypesInContext(ConsTypeList(head, tail), gamma)
    =>
    TypeInContext(head, gamma)
    & TypesInContext(tail, gamma)
    ;

Pred ContextExtension : TypeContext * TypeContext;
Axiom
    ContextExtension(gamma_0, gamma_1)
    & TypeInContext(sigma, gamma_0)
    =>
    TypeInContext(sigma, gamma_1)
    ;

// ## Type contexts are closed under type formers and their inverses.
Axiom
    TypeInContext(FunctionType(dom, cod), gamma)
    =>
    TypesInContext(dom, gamma)
    & TypeInContext(cod, gamma)
    ;
Axiom
    kappa = FunctionType(dom, cod)
    & TypesInContext(dom, gamma)
    & TypeInContext(cod, gamma)
    =>
    TypeInContext(kappa, gamma)
    ;

Axiom
    gamma : TypeContext
    & sigma = VoidType()
    =>
    TypeInContext(sigma, gamma);
Axiom
    gamma : TypeContext
    & sigma = BooleanType()
    =>
    TypeInContext(sigma, gamma);
Axiom
    gamma : TypeContext
    & sigma = NumberType()
    =>
    TypeInContext(sigma, gamma);
Axiom
    gamma : TypeContext
    & sigma = StringType()
    =>
    TypeInContext(sigma, gamma);

// ## The type contexts associated with expressions and statements.
Func ModuleTypeContext : ModuleNode -> TypeContext;
Func FunctionTypeContext : FunctionNode -> TypeContext;
Func ArgListContext: ArgList -> TypeContext;
Func StmtTypeContext : StmtNode -> TypeContext;
Func StmtsTypeContext : StmtNodeList -> TypeContext;
Func ExprTypeContext : ExprNode -> TypeContext;
Func ExprsTypeContext : ExprNodeList -> TypeContext;

Axiom mn : ModuleNode => ModuleTypeContext(mn)!;
Axiom fn : FunctionNode => FunctionTypeContext(fn)!;
Axiom args : ArgList => ArgListContext(args)!;
Axiom stmt : StmtNode => StmtTypeContext(stmt)!;
Axiom stmts : StmtNodeList => StmtsTypeContext(stmts)!;
Axiom expr : ExprNode => ExprTypeContext(expr)!;
Axiom exprs : ExprNodeList => ExprsTypeContext(exprs)!;

// ## Populating the TypeInContext predicate.

// Forward declarations.
Func ExprType : ExprNode -> Type;
Sort GeneralType;
Func VarTypeInArgList : Var * ArgList -> GeneralType;
Func MonoType : Type -> GeneralType;

// The type of an expression is in the type context of that expression.
Axiom
    sigma = ExprType(expr)
    & ctx = ExprTypeContext(expr)
    =>
    TypeInContext(sigma, ctx)
    ;

// The type of a function argument is in the context of the arg list of the function.
Axiom
    ConsArgList(arg_list, head_var, _, tail)
    // The type of a variable is only in recorded after the variable has been introduced.
    & MonoType(sigma) = VarTypeInArgList(head_var, tail)
    & ctx = ArgListContext(arg_list)
    =>
    TypeInContext(sigma, ctx)
    ;

// Relate type contexts of node lists with the type contexts of individual nodes in the lists.
Axiom
		ConsArgList(al, _, _, tail)
    & ArgListContext(al) = ctx
    =>
    & ArgListContext(tail) = ctx
    ;
Axiom
		ConsStmtNodeList(stmts, head, tail)
    & StmtsTypeContext(stmts) = ctx
    =>
    StmtTypeContext(head) = ctx
    & StmtsTypeContext(tail) = ctx
    ;
Axiom
		ConsExprNodeList(stmts, head, tail)
    & ExprsTypeContext(stmts) = ctx
    =>
    ExprTypeContext(head) = ctx
    & ExprsTypeContext(tail) = ctx
    ;

// Module type contexts propagate into top level statements.
Axiom
    Module(mn, stmts)
    & ctx = ModuleTypeContext(mn)
    =>
    StmtsTypeContext(stmts) = ctx
    ;

// Function type contexts propagate into bodies.
Axiom
    Function(fn, _, arg_list, _, stmts)
    & ctx = FunctionTypeContext(fn)
    =>
    StmtsTypeContext(stmts) = ctx
    & ArgListContext(arg_list) = ctx
    ;

// ## Propagate type contexts through statements.
Axiom
    ExprStmtNode(stmt, expr)
    =>
    ExprTypeContext(expr) ~> StmtTypeContext(stmt)
    ;
Axiom
    LetStmtNode(stmt, _, _, expr)
    =>
    ExprTypeContext(expr) ~> StmtTypeContext(stmt)
    ;
Axiom
    ReturnStmtNode(stmt, expr)
    =>
    ExprTypeContext(expr) ~> StmtTypeContext(stmt)
    ;
Axiom
    FunctionStmtNode(stmt, func)
    & ambient_ctx = StmtTypeContext(stmt)
    & func_ctx = FunctionTypeContext(func)
    =>
    ContextExtension(ambient_ctx, func_ctx)
    ;
Axiom
    IfStmtNode(stmt, cond, true_branch, false_branch)
    & stmt_ctx = StmtTypeContext(stmt)
    & cond_ctx = ExprTypeContext(cond)
    & true_branch_ctx = StmtsTypeContext(true_branch)
    & false_branch_ctx = StmtsTypeContext(false_branch)
    =>
    stmt_ctx = cond_ctx
    & cond_ctx = true_branch_ctx
    & true_branch_ctx = false_branch_ctx
    ;
Axiom
    WhileStmtNode(stmt, cond, body)
    & stmt_ctx = StmtTypeContext(stmt)
    & cond_ctx = ExprTypeContext(cond)
    & body_ctx = StmtsTypeContext(body)
    =>
    stmt_ctx = cond_ctx
    & cond_ctx = body_ctx
    ;

// ## Propagate type contexts through expressions.

Axiom
    AppExprNode(expr, fn_expr, arg_exprs)
    & expr_ctx = ExprTypeContext(expr)
    & fn_ctx = ExprTypeContext(fn_expr)
    & args_ctx = ExprsTypeContext(arg_exprs)
    =>
    expr_ctx = fn_ctx
    & fn_ctx = args_ctx
    ;

Axiom
    FunctionExprNode(expr, func)
    & expr_ctx = ExprTypeContext(expr)
    & func_ctx = FunctionTypeContext(func)
    =>
    ContextExtension(expr_ctx, func_ctx)
    ;

// ## Type substitution.
Sort Substitution;

Func SubstitutionBase : Substitution -> TypeContext;

Func Substitute : Substitution * Type -> Type;
Func Substitutes : Substitution * TypeList -> TypeList;

Axiom NilTypeList() ~> Substitutes(_, NilTypeList());
Axiom
    Substitutes(subst, ConsTypeList(head, tail))!
    =>
    Substitute(subst, head)!
    & Substitutes(subst, tail)!
    ;

Axiom
    ConsTypeList(Substitute(subst, head), Substitutes(subst, tail))
    ~>
    Substitutes(subst, ConsTypeList(head, tail));

// ## Substitution laws.
Axiom
    tau = Substitute(subst, sigma)
    & TypeInContext(sigma, SubstitutionBase(subst))
    =>
    sigma = tau
    ;

Axiom VoidType() ~> Substitute(_, VoidType());
Axiom BooleanType() ~> Substitute(_, BooleanType());
Axiom NumberType() ~> Substitute(_, NumberType());
Axiom StringType() ~> Substitute(_, StringType());

Axiom
    Substitute(subst, FunctionType(dom, cod))!
    =>
    Substitutes(subst, dom)!
    & Substitute(subst, cod)!
    ;
Axiom
    FunctionType(Substitutes(subst, dom), Substitute(subst, cod))
    ~>
    Substitute(subst, FunctionType(dom, cod))
    ;


// # Variable resolution.
//
// For each node kind X in which variables matter, we introduce a partial function
//
//   VarTypeInX : Var * X -> Type
//
// and add axioms such that VarTypeInX(var, n) = sigma iff var is considered to be in scope for
// node n. sigma can be an opaque Type element. That var must be in scope for n means that var must
// have been introduced *before* n; it is not sufficient for n to introduce the variable.
//
// For X = StmtNodeList and X = ArgList, note that if n = ConsX(head, tail), then head appears
// before tail in source code. For example, if body is the StmtNodeList representing the body of a
// function, then none of the variables introduced in the body will be in scope for body. Instead,
// all of those variables are in scope at the NilStmtNodeList of body. Similarly, if args is the
// ArgList of a function definition, then the variables listed in args are only in scope for the
// NilArgList at the root of args.

Func PolyType : TypeContext * Type -> GeneralType;
// Declared earlier:
// Func MonoType : Type -> GeneralType;
Axiom sigma : Type => MonoType(sigma)!;

Func VarTypeInStmts : Var * StmtNodeList -> GeneralType;
Func VarTypeInFunction : Var * FunctionNode -> GeneralType;
// Declared earlier:
// Func VarTypeInArgList : Var * ArgList -> GeneralType;
Func VarTypeInExpr : Var * ExprNode -> GeneralType;
Func VarTypeInExprs : Var * ExprNodeList -> GeneralType;

// Forward declarations of the function assigning semantic types to function literals.
Func FunctionNodeType : FunctionNode -> Type;

// ## Base cases: Nodes that introduce variables

// Every let statements introduces a variable.
Axiom
    ConsStmtNodeList(_, head, tail)
    & LetStmtNode(head, var, tno, expr)
    & expr_type = ExprType(expr)
    & mono_expr_type = MonoType(expr_type)
    =>
    expr_type = SemanticTypeOpt(tno)
    & VarTypeInStmts(var, tail) = mono_expr_type
    ;

// Every function statement introduces a variable for statements occuring after the function
// statement.
Axiom
    ConsStmtNodeList(_, head, _)
    & FunctionStmtNode(head, func)
    & ctx = StmtTypeContext(head)
    & Function(func, _, _, _ ,_)
    =>
    PolyType(ctx, FunctionNodeType(func))!
    ;
Axiom
    ConsStmtNodeList(_, head, tail)
    & FunctionStmtNode(head, func)
    & ctx = StmtTypeContext(head)
    & Function(func, var, _, _ ,_)
    =>
    VarTypeInStmts(var, tail)
    ~>
    PolyType(ctx, FunctionNodeType(func))
    ;

// Every function node introduces the name of the function in the ArgList of the function (and
// hence, by the propagation axioms below, also in the body of the function).
Axiom
    Function(func, var, arg, _ ,_)
    =>
    VarTypeInArgList(var, arg)
    ~>
    MonoType(FunctionNodeType(func))
    ;

// Every function argument introduces a variable.
Axiom
    ConsArgList(_, var, tno, tail)
    =>
    VarTypeInArgList(var, tail)
    ~>
    MonoType(SemanticTypeOpt(tno))
    ;

// ## Errors due to multiple introductions of the same variable
//
// For each base node case that introduces a variable, it is an error if the variable is already in
// scope before the node.

Axiom LetStmtNode(head, var, _, _) & VarTypeInStmts(var, stmts)! & ConsStmtNodeList(stmts, head, _) => ConflictingVariables();
Axiom
    FunctionStmtNode(head, func)
    & Function(func, var, _, _ ,_)
    & VarTypeInStmts(var, stmts)!
		& ConsStmtNodeList(stmts, head, _)
    =>
    ConflictingVariables()
    ;
Axiom Function(func, var, _, _, _) & VarTypeInFunction(var, func)! => ConflictingVariables();
Axiom VarTypeInArgList(var, al)! & ConsArgList(al, var, _, _) => ConflictingVariables();

// ## Propagation of variables through lists
//
// If a variable is in scope for ConsX(head, tail), then it should also be in scope for tail.

Axiom ConsStmtNodeList(stmts, _, tail) => VarTypeInStmts(var, tail) ~> VarTypeInStmts(var, stmts);
Axiom ConsArgList(al, _, _, tail) => VarTypeInArgList(var, tail) ~> VarTypeInArgList(var, al);
Axiom
	VarTypeInExprs(var, exprs) = sigma &
	ConsExprNodeList(exprs, head, tail)
	=>
	VarTypeInExpr(var, head) = sigma
  & VarTypeInExprs(var, tail) = sigma
  ;

// ## Propagating variables through nodes
//
// If a variable is in scope before some node n, then it is usually also in scope at all nodes that
// make up n.

// ### Propagating into function bodies
//
// Apart from the variables propagated from outside of the scope of a function literal (which will
// be propagated later), the variables accessible in a function body are as follows:
// 1. The variables defined in the ambient scope of the function definition.
// 2. The name of the function (for recursive calls).
// 3. Function arguments.
//
// We propagate 1 and 2 into 3, and 3 into the function body.

// Propagate ambient variables into arg lists.
Axiom
    Function(func, _, args, _, _)
    =>
    VarTypeInArgList(var, args)
    ~>
    VarTypeInFunction(var, func)
    ;

// By the "root" of a node list l we mean the unique nil node from which l can be obtained by
// repeated consing. We need access to the root of an ArgList so that we know the full set of
// variables introduced in that arg list.

// The axioms are such that RootNilArgList is total if every arg list is Nil or Cons (and not
// opaque).
Func RootNilArgList : ArgList -> ArgList;
Axiom NilArgList(args) => RootNilArgList(args) = args;
Axiom ConsArgList(al, _, _, tail) => RootNilArgList(al) ~> RootNilArgList(tail);

// Propagate variables from the arg list into the body.
Axiom
    Function(_, _, args, _, body)
    =>
    VarTypeInStmts(var, body)
    ~>
    VarTypeInArgList(var, RootNilArgList(args))
    ;

// ### Propagating variables through statements

// If a variable is in scope in the statement that defines a function, then the variable is also in
// scope of the ArgList of that function (which then means that it is in scope for the body).
Axiom
    ConsStmtNodeList(stmts, head, _)
    & FunctionStmtNode(head, func)
    =>
    VarTypeInFunction(var, func)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// Propagate variables through if statements.
Axiom
    ConsStmtNodeList(stmts, head, _)
    & IfStmtNode(head, cond, true_branch, false_branch)
    & VarTypeInStmts(var, stmts) = sigma
    =>
    VarTypeInExpr(var, cond) = sigma
    & VarTypeInStmts(var, true_branch) = sigma
    & VarTypeInStmts(var, false_branch) = sigma
    ;

// Propagate variables through while statements.
Axiom
    ConsStmtNodeList(stmts, head, _)
    & WhileStmtNode(head, cond, body)
    & sigma = VarTypeInStmts(var, stmts)
    =>
    VarTypeInStmts(var, body) = sigma
    & VarTypeInExpr(var, cond) = sigma
    ;

// Propagate variables through expression statements.
Axiom
    ConsStmtNodeList(stmts, head, _)
    & ExprStmtNode(head, expr)
    =>
    VarTypeInExpr(var, expr)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// Propagate previously defined variables through let statements into initializer expressions.
Axiom
    ConsStmtNodeList(stmts, head, _)
    & LetStmtNode(head, _, _, expr)
    =>
    VarTypeInExpr(var, expr)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// Propagate variables through return statements.
Axiom
    ConsStmtNodeList(stmts, head, _)
    & ReturnStmtNode(head, expr)
    =>
    VarTypeInExpr(var, expr)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// ### Propagating variables through expressions

// Propagate variables through function application expressions.
Axiom
    AppExprNode(expr, func, args)
    & VarTypeInExpr(var, expr) = sigma
    =>
    VarTypeInExpr(var, func) = sigma
    & VarTypeInExprs(var, args) = sigma
    ;

// Propagate variables through function expression.
Axiom
    FunctionExprNode(expr, func)
    =>
    VarTypeInFunction(var, func)
    ~>
    VarTypeInExpr(var, expr)
    ;

// # Expression typing.
// Declared earlier:
// Func ExprType : ExprNode -> Type;
Func ExprTypes : ExprNodeList -> TypeList;

// The substitutions associated to expressions with PolyTypes that must be instantiated into
// MonoTypes.
Func ExprSubstitution : ExprNode -> Substitution;

// Both ExprType and ExprTypes are total. Our axioms enforce that ExprTypes is given by mapping
// ExprType on each ExprNode element.
Axiom expr: ExprNode => ExprType(expr)!;
Axiom exprs: ExprNodeList => ExprTypes(exprs)!;

Axiom NilExprNodeList(exprs) => NilTypeList() ~> ExprTypes(exprs);
Axiom
    ConsExprNodeList(exprs, head, tail)
    =>
    ConsTypeList(ExprType(head), ExprTypes(tail))
    ~>
    ExprTypes(exprs)
    ;

// ## Typing constraints from usage of expressions in statements.
Axiom IfStmtNode(_, cond, _, _) => BooleanType() ~> ExprType(cond);
Axiom WhileStmtNode(_, cond, _) => BooleanType() ~> ExprType(cond);

// ## Typing constraints of expression from usages in other expression.
Axiom VariableExprNode(expr, var) & VarTypeInExpr(var, expr) = MonoType(sigma) => ExprType(expr) = sigma;

Axiom
    VariableExprNode(expr, var)
    & VarTypeInExpr(var, expr) = PolyType(_, _)
    =>
    ExprSubstitution(expr)!
    ;
Axiom
    VariableExprNode(expr, var)
    & VarTypeInExpr(var, expr) = PolyType(ctx, _)
    =>
    SubstitutionBase(ExprSubstitution(expr)) ~> ctx
    ;
Axiom
    VariableExprNode(expr, var)
    & VarTypeInExpr(var, expr) = PolyType(_, sigma)
    =>
    Substitute(ExprSubstitution(expr), sigma)
    ~>
    ExprType(expr)
    ;

Axiom VoidExprNode(expr) => VoidType() ~> ExprType(expr);
Axiom FalseExprNode(expr) => BooleanType() ~> ExprType(expr);
Axiom TrueExprNode(expr) => BooleanType() ~> ExprType(expr);
Axiom StringLiteralExprNode(expr, _) => StringType() ~> ExprType(expr);
Axiom NumberLiteralExprNode(expr, _) => NumberType() ~> ExprType(expr);

// The conclusions of these axioms assert that DomainTypes or CodomainType are defined on some
// types tau. By the axioms relating these functions to FunctionType, this means that such types
// tau must be function types.
Axiom AppExprNode(_, func, args) => DomainTypes(ExprType(func)) ~> ExprTypes(args);
Axiom AppExprNode(expr, func, _) => CodomainType(ExprType(func)) ~> ExprType(expr);

Axiom
    FunctionExprNode(expr, _)
    =>
    ExprSubstitution(expr)!
    ;
Axiom
    FunctionExprNode(expr, _)
    & ctx = ExprTypeContext(expr)
    =>
    SubstitutionBase(ExprSubstitution(expr)) ~> ctx
    ;

Axiom
    FunctionExprNode(expr, func)
    =>
    Substitute(ExprSubstitution(expr), FunctionNodeType(func))
    ~>
    ExprType(expr)
    ;

// # Type analysis of function definitions
//
// ## Reachability
//
// To detect whether a function has an implicit `return ();` in the end, we need to decide whether
// the execution flow can reach the end of the function. Obviously this is undecidable in the
// general case. Our type system will thus consider all possible paths through branches of function
// body possible independently of which values expressions can assume. For example, in the function
// 
//   function foo () {
//     if (true) {
//       ...
//     } else {
//       ...
//     }
//   }
//
// we will consider the `else` branch potentially reachable.

// CanProceedStmt(stmt) is true if execution can flow past this statement. CanProceedStmts(stmts)
// is true if CanProceed holds for all statements in stmts.
Pred CanProceedStmt : StmtNode;
Pred CanProceedStmts : StmtNodeList;

Axiom NilStmtNodeList(stmts) => CanProceedStmts(stmts);
Axiom
    ConsStmtNodeList(stmts, head, tail)
    & CanProceedStmt(head)
    & CanProceedStmts(tail)
    =>
    CanProceedStmts(stmts);

Axiom ExprStmtNode(stmt, _) => CanProceedStmt(stmt);
Axiom LetStmtNode(stmt, _, _, _) => CanProceedStmt(stmt);
// ReturnStmtNode does not proceed.
// ReturnVoidStmtNode does not proceed.
Axiom FunctionStmtNode(stmt, _) => CanProceedStmt(stmt);
// An IfStmtNode can proceed if at least one of the two branches can proceed.
Axiom IfStmtNode(stmt, _, true_branch, _) & CanProceedStmts(true_branch) => CanProceedStmt(stmt);
Axiom IfStmtNode(stmt, _, _, false_branch) & CanProceedStmts(false_branch) => CanProceedStmt(stmt);
Axiom WhileStmtNode(stmt, _, _) => CanProceedStmt(stmt);

// ## Explicit return type analysis
//
// Here we analyze which types the expression in explicit return statements among a list of
// statements have. The list of statements need not be the full body of a function, so we only care
// about explicit returns, not the possible implicit `return ();` at the end of a function.
// However, every list of statements can at most return expressions of a single type, so we can
// model `ReturnsType` as a partial function.
Func ReturnsType : StmtNodeList -> Type;

// If there is any suffix of a statement list that can return a given type, then the full list can
// return that type.
Axiom ConsStmtNodeList(stmts, _, tail) => ReturnsType(stmts) ~> ReturnsType(tail);

// Base cases for return statements with and without (i.e. void) expressions.
Axiom
    ConsStmtNodeList(stmts, head, _)
    & ReturnStmtNode(head, return_value)
    =>
    ReturnsType(stmts) ~> ExprType(return_value)
    ;
Axiom
    ConsStmtNodeList(stmts, head, _)
    & ReturnVoidStmtNode(head)
    =>
    VoidType()!
    & ReturnsType(stmts) = VoidType();

// An if statement can return a type if at least one of its two branches returns that type.
Axiom
    ConsStmtNodeList(stmts, head, _)
    & IfStmtNode(head, _, true_branch, _)
    =>
    ReturnsType(stmts) ~> ReturnsType(true_branch)
    ;
Axiom
    ConsStmtNodeList(stmts, head, _)
    & IfStmtNode(head, _, _, false_branch)
    =>
    ReturnsType(stmts) ~> ReturnsType(false_branch)
    ;

// A while statement can return a type if its body can return that type.
Axiom
    ConsStmtNodeList(stmts, head, _)
    & WhileStmtNode(head, _, body)
    =>
    ReturnsType(stmts) ~> ReturnsType(body)
    ;

// ## Tying it together.

// Declared earlier:
// FunctionNodeType : FunctionNode -> Type

// FunctionNodeType is total and always yields a function type.
Axiom
    func: FunctionNode
    =>
    FunctionNodeType(func)!
    & DomainTypes(FunctionNodeType(func))!
    & CodomainType(FunctionNodeType(func))!
    ;

// Typing constraints due to the types of function variables. Note that the types of argument
// variables as given by VarInArgList were previously unified with the optional type annotation (an
// opaque Type element if no annotation was given), which in turn were unified with the types
// appearing in SemanticArgTypes.
Axiom
    Function(func, _, args, _, _)
    =>
    SemanticArgTypes(args)
    ~>
    DomainTypes(FunctionNodeType(func))
    ;

// Typing constraints due to an explicit codomain annotation.
Axiom
    Function(func, _, _, codomain, _)
    =>
    SemanticTypeOpt(codomain) ~> CodomainType(FunctionNodeType(func))
    ;

// Typing constraints due to explicit return statements.
Axiom
    Function(func, _, _, _, body)
    =>
    ReturnsType(body)
    ~>
    CodomainType(FunctionNodeType(func))
    ;

// An implicit Void return type in case we cannot infer that the function body must
// always reach an explicit return statement.
Axiom
    Function(func, _, _, _, body)
    & CanProceedStmts(body)
    =>
    VoidType()
    ~>
    CodomainType(FunctionNodeType(func))
    ;
