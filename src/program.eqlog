Pred Absurd: ();

// # The AST, encoded algebraically/relationally.
Sort TypeNode;
Sort TypeNodeOpt;

Sort ExprNode;
Sort ExprNodeList;

Sort StmtNode;
Sort StmtNodeList;

Sort FunctionNode;

Sort Var;
Sort StringLiteral;
Sort NumberLiteral;

Sort ArgList;

Pred VoidTypeNode : TypeNode;
Pred BooleanTypeNode : TypeNode;
Pred NumberTypeNode : TypeNode;
Pred StringTypeNode : TypeNode;
// FunctionTypeNode(node, domain, codomain)
Pred FunctionTypeNode : TypeNode * ArgList * TypeNode;

Pred VariableExprNode : ExprNode * Var;
Pred VoidExprNode : ExprNode;
Pred FalseExprNode : ExprNode;
Pred TrueExprNode : ExprNode;
Pred StringLiteralExprNode : ExprNode * StringLiteral;
Pred NumberLiteralExprNode : ExprNode * NumberLiteral;
// AppExprNode(node, func, args)
Pred AppExprNode : ExprNode * ExprNode * ExprNodeList;
Pred FunctionExprNode : ExprNode * FunctionNode;

Pred ExprStmtNode : StmtNode * ExprNode;
// LetStmtNode(node, var, initial_value)
Pred LetStmtNode : StmtNode * Var * ExprNode;
// ReturnStmtNode(node, return_value)
Pred ReturnStmtNode : StmtNode * ExprNode;
Pred ReturnVoidStmtNode : StmtNode;
Pred FunctionStmtNode : StmtNode * FunctionNode;
// IfStmtNode(node, condition, true_branch, false_branch)
Pred IfStmtNode : StmtNode * ExprNode * StmtNodeList * StmtNodeList;
// WhileStmtNode(node, condition, body)
Pred WhileStmtNode : StmtNode * ExprNode * StmtNodeList;

// Function(node, function_name, domain, codomain, body)
Pred Function : FunctionNode * Var * ArgList * TypeNodeOpt * StmtNodeList;

// ## <Sort>NodeList operators.
Pred NilExprNodeList : ExprNodeList;
Func ConsExprNodeList : ExprNode * ExprNodeList -> ExprNodeList;

Pred NilStmtNodeList : StmtNodeList;
Func ConsStmtNodeList : StmtNode * StmtNodeList -> StmtNodeList;

// ## TypeNodeOpt operators.
Pred NoneTypeNodeOpt : TypeNodeOpt;
Func SomeTypeNodeOpt : TypeNode -> TypeNodeOpt;

// ## ArgList operators.
Pred NilArgList : ArgList;
Func ConsArgList : Var * TypeNodeOpt * ArgList -> ArgList;


// # Semantic types.
Sort Type;
Sort TypeList;

// ## Semantic type operators.
Func VoidType : Type;
Func BooleanType : Type;
Func NumberType : Type;
Func StringType : Type;
// FunctionType(node, domain, codomain)
Func FunctionType : TypeList * Type -> Type;
// Inverses to FunctionType:
// - DomainTypes(FunctionType(domain, _)) = domain
// - CodomainType(FunctionType(_, codomain)) = codomain
Func DomainTypes : Type -> TypeList;
Func CodomainType : Type -> Type;

Axiom DomainTypes(tau)! => CodomainType(tau)!;
Axiom CodomainType(tau)! => DomainTypes(tau)!;
Axiom DomainTypes(FunctionType(domain, _)) ~> domain;
Axiom CodomainType(FunctionType(_, codomain)) ~> codomain;
Axiom FunctionType(DomainTypes(tau), CodomainType(tau)) ~> tau;

// ## TypeList operators.
// We're equating TypeList elements sometimes. In well-typed programs, this is only possible if the
// type lists have the same length and are made up of the same Types.
Func NilTypeList : TypeList;
Func ConsTypeList : Type * TypeList -> TypeList;
Axiom NilTypeList() = ConsTypeList(_, _) => Absurd();
Axiom ConsTypeList(head_0, tail_0) = ConsTypeList(head_1, tail_1) => head_0 = head_1 & tail_0 = tail_1;

// ## Joint injectivity of type operators.
Axiom VoidType() = BooleanType() => Absurd();
Axiom VoidType() = NumberType() => Absurd();
Axiom VoidType() = StringType() => Absurd();
Axiom VoidType() = FunctionType(_, _) => Absurd();

Axiom BooleanType() = NumberType() => Absurd();
Axiom BooleanType() = StringType() => Absurd();
Axiom BooleanType() = FunctionType(_, _) => Absurd();

Axiom NumberType() = StringType() => Absurd();
Axiom NumberType() = FunctionType(_, _) => Absurd();

Axiom StringType() = FunctionType(_, _) => Absurd();

Axiom
    FunctionType(domain_0, codomain_0) = FunctionType(domain_1, codomain_1)
    =>
    domain_0 = domain_1
    & codomain_0 = codomain_1
    ;

// ## Functions assigning to node elements their corresponding semantic Type elements.
Func SemanticType : TypeNode -> Type;
Func SemanticTypeOpt : TypeNodeOpt -> Type;
Func SemanticArgTypes : ArgList -> TypeList;

Axiom tn: TypeNode => SemanticType(tn)!;
Axiom tno: TypeNodeOpt => SemanticTypeOpt(tno)!;
Axiom ag: ArgList => SemanticArgTypes(ag)!;

// Since another axiom enforces that SemanticType is total, these rules will fire for all
// TypeNodes.
Axiom VoidTypeNode(tn) => VoidType() ~> SemanticType(tn);
Axiom BooleanTypeNode(tn) => BooleanType() ~> SemanticType(tn);
Axiom NumberTypeNode(tn) => NumberType() ~> SemanticType(tn);
Axiom StringTypeNode(tn) => StringType() ~> SemanticType(tn);
// Other axioms assert that SemanticArgTypes and SemanticType are total functions, so this rule
// will fire for al FunctionTypeNodes.
Axiom
    FunctionTypeNode(tn, args, codomain)
    =>
    FunctionType(SemanticArgTypes(args), SemanticType(codomain))
    ~>
    SemanticType(tn)
    ;

// SemanticTypeOpt is total, but only for SomeTypeNodeOpt elements does it have a determined type.
// If no other rules apply, it will be an opaque Type element determined only by the TypeNodeOpt
// element.
Axiom SemanticType(tn) ~> SemanticTypeOpt(SomeTypeNodeOpt(tn));

Axiom NilArgList(al) => NilTypeList() ~> SemanticArgTypes(al);
Axiom
    al = ConsArgList(_, head_type, tail)
    =>
    ConsTypeList(SemanticTypeOpt(head_type), SemanticArgTypes(tail))
    ~>
    SemanticArgTypes(al)
    ;


// # Variable resolution.
//
// For each node kind X in which variables matter, we introduce a partial function
//
//   VarTypeInX : Var * X -> Type
// 
// and add axioms such that VarTypeInX(var, n) = sigma iff var is considered to be in scope for
// node n. sigma can be an opaque Type element. That var must be in scope for n means that var must
// have been introduced *before* n; it is not sufficient for n to introduce the variable.
//
// For X = StmtNodeList and X = ArgList, note that if n = ConsX(head, tail), then head appears
// before tail in source code. For example, if body is the StmtNodeList representing the body of a
// function, then none of the variables introduced in the body will be in scope for body. Instead,
// all of those variables are in scope at the NilStmtNodeList of body. Similarly, if args is the
// ArgList of a function definition, then the variables listed in args are only in scope for the
// NilArgList at the root of args. 
Func VarTypeInStmts : Var * StmtNodeList -> Type;
Func VarTypeInFunction : Var * FunctionNode -> Type;
Func VarTypeInArgList : Var * ArgList -> Type;
Func VarTypeInExpr : Var * ExprNode -> Type;
Func VarTypeInExprs : Var * ExprNodeList -> Type;

// Forward declaration of the function assigning semantic types to expression and function
// literals.
Func ExprType : ExprNode -> Type;
Func FunctionNodeType : FunctionNode -> Type;

// ## Base cases: Nodes that introduce variables

// Every let statements introduces a variable.
Axiom
    ConsStmtNodeList(head, tail)!
    & LetStmtNode(head, var, expr)
    =>
    VarTypeInStmts(var, tail)
    ~>
    ExprType(expr)
    ;

// Every function statement introduces a variable for statements occuring after the function
// statement.
Axiom
    ConsStmtNodeList(head, tail)!
    & FunctionStmtNode(head, func)
    & Function(func, var, _, _ ,_)
    =>
    VarTypeInStmts(var, tail)
    ~>
    FunctionNodeType(func)
    ;

// Every function node introduces the name of the function in the ArgList of the function (and
// hence, by the propagation axioms below, also in the body of the function).
Axiom
    Function(func, var, arg, _ ,_)
    =>
    VarTypeInArgList(var, arg)
    ~>
    FunctionNodeType(func)
    ;

// Every function argument introduces a variable.
Axiom
    ConsArgList(var, tno, tail)!
    =>
    VarTypeInArgList(var, tail)
    ~>
    SemanticTypeOpt(tno)
    ;

// ## Errors due to multiple introductions of the same variable
//
// For each base node case that introduces a variable, it is an error if the variable is already in
// scope before the node.

Axiom LetStmtNode(head, var, _) & VarTypeInStmts(var, ConsStmtNodeList(head, _))! => Absurd();
Axiom
    FunctionStmtNode(head, func)
    & Function(func, var, _, _ ,_)
    & VarTypeInStmts(var, ConsStmtNodeList(head, _))!
    =>
    Absurd()
    ;
Axiom Function(func, var, _, _, _) & VarTypeInFunction(var, func)! => Absurd();
Axiom VarTypeInArgList(var, ConsArgList(var, _, _))! => Absurd();

// ## Propagation of variables through lists
//
// If a variable is in scope for ConsX(head, tail), then it should also be in scope for tail.

Axiom VarTypeInStmts(var, tail) ~> VarTypeInStmts(var, ConsStmtNodeList(_, tail));
Axiom VarTypeInArgList(var, tail) ~> VarTypeInArgList(var, ConsArgList(_, _, tail));
Axiom VarTypeInExprs(var, tail) ~> VarTypeInExprs(var, ConsExprNodeList(_, tail));

// ## Propagating variables through nodes
//
// If a variable is in scope before some node n, then it is usually also in scope at all nodes that
// make up n.

// ### Propagating into function bodies
//
// Apart from the variables propagated from outside of the scope of a function literal (which will
// be propagated later), the variables accessible in a function body are as follows:
// 1. The variables defined in the ambient scope of the function definition.
// 2. The name of the function (for recursive calls).
// 3. Function arguments.
//
// We propagate 1 and 2 into 3, and 3 into the function body.

// Propagate ambient variables into arg lists.
Axiom
    Function(func, _, args, _, _)
    =>
    VarTypeInArgList(var, args)
    ~>
    VarTypeInFunction(var, func)
    ;

// Propagate function names into arg lists.
Axiom
    Function(func, var, args, _, _)
    =>
    VarTypeInArgList(var, args)
    ~>
    FunctionNodeType(func)
    ;

// By the "root" of a node list l we mean the unique nil node from which l can be obtained by
// repeated consing. We need access to the root of an ArgList so that we know the full set of
// variables introduced in that arg list.

// The axioms are such that RootNilArgList is total if every arg list is Nil or Cons (and not
// opaque).
Func RootNilArgList : ArgList -> ArgList;
Axiom NilArgList(args) => RootNilArgList(args) = args;
Axiom RootNilArgList(ConsArgList(_, _, tail)) ~> RootNilArgList(tail);

// Propagate variables from the arg list into the body.
Axiom
    Function(_, _, args, _, body)
    =>
    VarTypeInStmts(var, body)
    ~>
    VarTypeInArgList(var, RootNilArgList(args))
    ;

// ### Propagating variables through statements

// If a variable is in scope in the statement that defines a function, then the variable is also in
// scope of the ArgList of that function (which then means that it is in scope for the body).
Axiom
    stmts = ConsStmtNodeList(head, _)
    & FunctionStmtNode(head, func)
    =>
    VarTypeInFunction(var, func)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// Propagate variables through if statements.
Axiom
    stmts = ConsStmtNodeList(head, _)
    & IfStmtNode(head, cond, true_branch, false_branch)
    & VarTypeInStmts(var, stmts) = sigma
    =>
    VarTypeInExpr(var, cond) = sigma
    & VarTypeInStmts(var, true_branch) = sigma
    & VarTypeInStmts(var, false_branch) = sigma
    ;

// Propagate variables through while statements.
Axiom
    stmts = ConsStmtNodeList(head, _)
    & WhileStmtNode(head, _, body)
    =>
    VarTypeInStmts(var, body)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// Propagate variables through expression statements.
Axiom
    stmts = ConsStmtNodeList(head, _)
    & ExprStmtNode(head, expr)
    =>
    VarTypeInExpr(var, expr)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// Propagate previously defined variables through let statements into initializer expressions.
Axiom
    stmts = ConsStmtNodeList(head, _)
    & LetStmtNode(head, _, expr)
    =>
    VarTypeInExpr(var, expr)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// Propagate variables through return statements.
Axiom
    stmts = ConsStmtNodeList(head, _)
    & ReturnStmtNode(head, expr)
    =>
    VarTypeInExpr(var, expr)
    ~>
    VarTypeInStmts(var, stmts)
    ;

// ### Propagating variables through expressions

// Propagate variables through function application expressions.
Axiom
    AppExprNode(expr, func, args)
    & VarTypeInExpr(var, expr) = sigma
    => 
    VarTypeInExpr(var, func) = sigma
    & VarTypeInExprs(var, args) = sigma
    ;

// Propagate variables through function expression.
Axiom
    FunctionExprNode(expr, func)
    =>
    VarTypeInFunction(var, func)
    ~>
    VarTypeInExpr(var, expr)
    ;

// # Expression typing.
// Declared earlier:
// Func ExprType : ExprNode -> Type;
Func ExprTypes : ExprNodeList -> TypeList;

// Both ExprType and ExprTypes are total. Our axioms enforce that ExprTypes is given by mapping
// ExprType on each ExprNode element.
Axiom expr: ExprNode => ExprType(expr)!;
Axiom exprs: ExprNodeList => ExprTypes(exprs)!;

Axiom NilExprNodeList(exprs) => NilTypeList() ~> ExprTypes(exprs);
Axiom
    exprs = ConsExprNodeList(head, tail)
    =>
    ConsTypeList(ExprType(head), ExprTypes(tail))
    ~>
    ExprTypes(exprs)
    ;

// ## Typing constraints from usage of expressions in statements.
Axiom IfStmtNode(_, cond, _, _) => BooleanType() ~> ExprType(cond);
Axiom WhileStmtNode(_, cond, _) => BooleanType() ~> ExprType(cond);

// ## Typing constraints of expression from usages in other expression.
Axiom VariableExprNode(expr, var) & VarTypeInExpr(var, expr) = sigma => ExprType(expr) = sigma;
Axiom VoidExprNode(expr) => VoidType() ~> ExprType(expr);
Axiom FalseExprNode(expr) => BooleanType() ~> ExprType(expr);
Axiom TrueExprNode(expr) => BooleanType() ~> ExprType(expr);
Axiom StringLiteralExprNode(expr, _) => StringType() ~> ExprType(expr);
Axiom NumberLiteralExprNode(expr, _) => NumberType() ~> ExprType(expr);

// The conclusions of these axioms assert that DomainTypes or CodomainType are defined on some
// types tau. By the axioms relating these functions to FunctionType, this means that such types
// tau must be function types.
Axiom AppExprNode(_, func, args) => DomainTypes(ExprType(func)) ~> ExprTypes(args);
Axiom AppExprNode(expr, func, _) => CodomainType(ExprType(func)) ~> ExprType(expr);

Axiom FunctionExprNode(expr, func) => FunctionNodeType(func) ~> ExprType(expr);

// # Type analysis of function definitions
//
// ## Reachability
//
// To detect whether a function has an implicit `return ();` in the end, we need to decide whether
// the execution flow can reach the end of the function. Obviously this is undecidable in the
// general case; our type system will thus consider this possible if there is a control flow path
// through the function that does not traverse a return statement.

// CanProceedStmt(stmt) is true if execution can flow past this statement. CanProceedStmts(stmts)
// is true if CanProceed holds for all statements in stmts.
Pred CanProceedStmt : StmtNode;
Pred CanProceedStmts : StmtNodeList;

Axiom NilStmtNodeList(stmts) => CanProceedStmts(stmts);
Axiom
    stmts = ConsStmtNodeList(head, tail)
    & CanProceedStmt(head)
    & CanProceedStmts(tail)
    => 
    CanProceedStmts(stmts);

Axiom ExprStmtNode(stmt, _) => CanProceedStmt(stmt);
Axiom LetStmtNode(stmt, _, _) => CanProceedStmt(stmt);
// ReturnStmtNode does not proceed.
// ReturnVoidStmtNode does not proceed.
Axiom FunctionStmtNode(stmt, _) => CanProceedStmt(stmt);
// An IfStmtNode can proceed if at least one of the two branches can proceed.
Axiom IfStmtNode(stmt, _, true_branch, _) & CanProceedStmts(true_branch) => CanProceedStmt(stmt);
Axiom IfStmtNode(stmt, _, _, false_branch) & CanProceedStmts(false_branch) => CanProceedStmt(stmt);
Axiom WhileStmtNode(stmt, _, _) => CanProceedStmt(stmt);

// ## Explicit teturn type analysis
// Here we analyze which types the expression in explicitly return statements among a list of
// statements have. The list of statements need not be the full body of a function, so we only care
// about explicit returns, not the possible implicit `return ();` at the end of a function.
// However, every list of statements can at most return expressions of a single type, so we can
// model `ReturnsType` as a partial function.
Func ReturnsType : StmtNodeList -> Type;

// If there is any suffix of a statement list that can return a given type, then the full list can
// return that type.
Axiom ReturnsType(ConsStmtNodeList(_, tail)) ~> ReturnsType(tail);

// Base cases for return statements with and without (i.e. void) expressions.
Axiom
    stmts = ConsStmtNodeList(head, _)
    & ReturnStmtNode(head, return_value)
    =>
    ReturnsType(stmts) ~> ExprType(return_value)
    ;
Axiom
    stmts = ConsStmtNodeList(head, _)
    & ReturnVoidStmtNode(head)
    =>
    VoidType()!
    & ReturnsType(stmts) = VoidType();

// An if statement can return a type if at least one of its two branches returns that type.
Axiom
    stmts = ConsStmtNodeList(head, _)
    & IfStmtNode(head, _, true_branch, _)
    =>
    ReturnsType(stmts) ~> ReturnsType(true_branch)
    ;
Axiom
    stmts = ConsStmtNodeList(head, _)
    & IfStmtNode(head, _, _, false_branch)
    =>
    ReturnsType(stmts) ~> ReturnsType(false_branch)
    ;

// An while statement can return a type if its body can return that type.
Axiom
    stmts = ConsStmtNodeList(head, _)
    & WhileStmtNode(head, _, body)
    =>
    ReturnsType(stmts) ~> ReturnsType(body)
    ;

// ## Tying it together.

// Declared earlier:
// FunctionNodeType : FunctionNode -> Type

// FunctionNodeType is total and always yields a function type.
Axiom
    func: FunctionNode
    =>
    FunctionNodeType(func)!
    & DomainTypes(FunctionNodeType(func))!
    & CodomainType(FunctionNodeType(func))!
    ;

// Typing constraints due to the types of function variables. Note that the types of argument
// variables as given by VarInArgList were previously unified with the optional type annotation (an
// opaque Type element if no annotation was given), which in turn were unified with the types
// appearing SemanticArgTypes.
Axiom
    Function(func, _, args, _, _)
    =>
    SemanticArgTypes(args)
    ~>
    DomainTypes(FunctionNodeType(func))
    ;
 
// Typing constraints due to an explicit codomain annotation.
Axiom
    Function(func, _, _, codomain, _)
    =>
    SemanticTypeOpt(codomain) ~> CodomainType(FunctionNodeType(func))
    ;

// Typing constraints due to explicit return statements.
Axiom
    Function(func, _, _, _, body)
    =>
    ReturnsType(body)
    ~>
    CodomainType(FunctionNodeType(func))
    ;

// An implicit Void return type in case we cannot infer that the function body must
// always reach an explicit return statement.
Axiom
    Function(func, _, _, _, body)
    & CanProceedStmts(body)
    =>
    VoidType()
    ~>
    CodomainType(FunctionNodeType(func))
    ;
